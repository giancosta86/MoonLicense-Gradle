/*ยง
  ===========================================================================
  MoonLicense - Gradle
  ===========================================================================
  Copyright (C) 2015 Gianluca Costa
  ===========================================================================
  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
  ===========================================================================
*/


buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }

        jcenter()
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
    }
}


plugins {
    id 'com.jfrog.bintray' version '1.1'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"


group = 'info.gianlucacosta.moonlicense'
archivesBaseName = 'moonlicense-gradle'
version = '1.0'


description = 'MoonLicense integration into the Gradle build process'


repositories {
    mavenLocal()

    jcenter()
}


dependencies {
    compile gradleApi()

    compile localGroovy()

    compile group: 'info.gianlucacosta.moonlicense', name: 'moonlicense', version: '1.0'
}




task('sourcesJar', type: Jar, dependsOn: 'classes') {
    classifier = 'sources'
    from project.sourceSets.main.allSource
}


task('groovydocJar', type: Jar, dependsOn: 'groovydoc') {
    classifier = 'javadoc'
    from project.groovydoc.destinationDir
}


artifacts {
    archives project.sourcesJar
    archives project.groovydocJar
}



uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                    url: "file://" + new File(project.buildDir, "mavenDeploy")
            )

            pom.project {
                name project.name
                description project.description

                inceptionYear '2015'
                url 'https://github.com/giancosta86/MoonLicense-Gradle'


                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Gianluca Costa'
                        email 'gianluca@gianlucacosta.info'
                    }
                }

                scm {
                    url 'git@github.com:giancosta86/MoonLicense-Gradle.git'
                    connection 'scm:git:git@github.com:giancosta86/MoonLicense-Gradle.git'
                    developerConnection 'scm:git:git@github.com:giancosta86/MoonLicense-Gradle.git'
                }

            }
        }
    }
}



bintray {
    if (project.hasProperty("bintrayKey")) {
        user = bintrayUser
        key = bintrayKey
    }

    filesSpec {
        from('build/mavenDeploy') {
            include "**/${project.version}/*.jar"
            include "**/${project.version}/*.pom"
        }
        into '.'
    }

    dryRun = false
    publish = false

    pkg {
        repo = 'Hephaestus'

        name = project.name
        desc = project.description

        websiteUrl = 'https://github.com/giancosta86/MoonLicense-Gradle'
        issueTrackerUrl = 'https://github.com/giancosta86/MoonLicense-Gradle/issues'
        vcsUrl = 'https://github.com/giancosta86/MoonLicense-Gradle.git'

        licenses = ['Apache-2.0']
        labels = ['licenses', 'notices', 'Gradle', 'plugin', 'extensible']

        publicDownloadNumbers = false

        version {
            name = project.version
            vcsTag = 'v' + project.version

            released = new Date()

            gpg {
                sign = true
            }

            mavenCentralSync {
                sync = false
            }
        }
    }
}


pluginBundle {
    website = 'https://github.com/giancosta86/MoonLicense-Gradle'
    vcsUrl = 'https://github.com/giancosta86/MoonLicense-Gradle'

    plugins {
        moonLicensePlugin {
            id = 'info.gianlucacosta.moonlicense'
            displayName = 'MoonLicense for Gradle'

            description = 'MoonLicense integration into the Gradle build process'
            tags = ['licenses', 'notices', 'Gradle', 'plugin', 'source files', 'extensible']
        }
    }
}

